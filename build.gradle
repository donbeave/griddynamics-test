version '0.1'
group 'com.zhokhov.interview.griddynamics'

apply plugin: 'java'

ext {
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-cli:commons-cli:1.3.1'

    testCompile 'junit:junit:4.+'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

// create a single Jar with all dependencies
task releaseJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Anagram Jar File',
                'Implementation-Version': version,
                'Main-Class': 'com.zhokhov.interview.griddynamics.App'
    }
    baseName = 'anagram'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task release(type: Copy, dependsOn: 'releaseJar') {
    from "build/libs/anagram-${project.version}.jar"
    into './'
}

task runApp(type: JavaExec) {
    String inputPath = project.hasProperty('inputFile') ? project.inputFile : 'example1.txt'
    String outputPath = project.hasProperty('outputFile') ? project.outputFile : 'result.txt'

    //standardInput = System.in

    classpath = sourceSets.main.runtimeClasspath

    main = 'com.zhokhov.interview.griddynamics.App'
    args = [inputPath, outputPath]
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}